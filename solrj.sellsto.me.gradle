import org.gradle.api.internal.file.UnionFileCollection
import org.gradle.api.internal.file.collections.SimpleFileCollection

apply plugin: 'scala'
apply from: "gradle/utilities.gradle"

//fix for scala-java cross compilation
//todo zhugrov a - it would be great to remove it
compileScala.taskDependencies.values = compileScala.taskDependencies.values - 'compileJava'
compileJava.dependsOn compileScala
compileJava.classpath = new UnionFileCollection(new SimpleFileCollection(compileScala.destinationDir), compileScala.classpath)


dependencies {
    //scala plugin dependencies
    scalaTools     'org.scala-lang:scala-compiler:2.10.0-M2'
    scalaTools     'org.scala-lang:scala-library:2.10.0-M2'

    //compile dependencies
    compile        project(':lucene')
    compile        project(':solr')
    compile        project(':modules:queries')
    compile        project(':modules:queryparser')
    compile        'org.json:json:20090211' //Q: do you really need this dependency?
    compile        'net.sourceforge.findbugs:jsr305:1.3.7'
    compile        'org.scala-lang:scala-library:2.10.0-M2'
    compile        fileTree(dir:'lib/local/compile', includes: ['reactive-core.jar'])
    compile        'javax.servlet:javax.servlet-api:3.0.1'
    compile        'org.slf4j:slf4j-api:1.6.1'
    compile        'org.slf4j:slf4j-jdk14:1.6.1'
    compile        'com.google.guava:guava:r05'
    compile        'commons-lang:commons-lang:2.6'
    compile        'org.apache.httpcomponents:httpcore:4.1.4'
    compile        'org.apache.httpcomponents:httpclient:4.1.3'
    compile        'commons-io:commons-io:2.1'
    compile        'commons-codec:commons-codec:1.6'

    //test dependencies
    testCompile     project(path: ':solr',   configuration: 'testFramework')
    testCompile     project(path: ':lucene', configuration: 'testFramework')
    testCompile     'junit:junit:4.10'
    testCompile     'org.easymock:easymock:3.1'
    testCompile     'org.powermock:powermock-api-easymock:1.4.11'
    testCompile     'org.powermock:powermock-module-javaagent:1.4.11'
    testCompile     'org.powermock:powermock-api-support:1.4.11'
    testCompile     'org.scalatest:scalatest_2.9.1:1.7.1'
}

sourceSets {
    main {
        scala {
            srcDir 'src'
        }
        java {
            srcDir 'src'
        }
    }

    test {
       scala {
            srcDirs 'test-src', 'test-framework/src'
       }
       java {
            srcDirs 'test-src', 'test-framework/src'
       }
       resources {
            srcDir 'test-files'
       }
    }
}

archivesBaseName = "sellstome-solr"

/** Allows to rebuild a solr configuration */
task configSolr(type: Copy, dependsOn: 'jar') {

    description = 'Updates the solr confguration on server'

    copy {
        from    'conf/solr'
        into    '/var/solr/sellstome'
    }

    /** copy library dependencies */
    copy {
        from configurations.compile
        into '/var/solr/sellstome/lib'
    }

    copy {
        from configurations.runtime
        into '/var/solr/sellstome/lib'
    }

    /** publish compiled artifacts */
    copy {
        from configurations.archives.allArtifacts.files
        into '/var/solr/sellstome/lib'
    }

}